module hott1 where
import algebra
import basic
import definitions1

-- exercise 1.1
compose (A B C : U) (g : B -> C) (f : A -> B) : A -> C =
  \(x : A) -> g (f x)

composeAssoc (A B C D : U) (h : C -> D) (g : B -> C) (f : A -> B) :
  Path (A -> D) (compose A C D h (compose A B C g f))
                (compose A B D (compose B C D h g) f) =
  refl (A -> D) (\(x : A) -> h (g (f x)))

-- exercise 1.2
recTupleFromPr (A B C : U) (g : A -> B -> C) (x : Tuple A B) : C =
  g (pr1Tuple A B x) (pr2Tuple A B x)

recExistsFromPr
  (A C : U) (B : A -> U)
  (g : (x : A) -> B x -> C) (p : (x : A) * B x) : C =
  g (pr1Exists A B p) (pr2Exists A B p)

-- exercise 1.3
indTupleFromPr
  (A B : U) (C : Tuple A B -> U)
  (g : (x : A) -> (y : B) -> C (pair x y))
  (x : Tuple A B) : C x =
  transport (<i> C (uniqTuple A B x @ i))
            (g (pr1Tuple A B x) (pr2Tuple A B x))

uniqExists (A : U) (B : A -> U) (x : (a : A) * (B a)) :
         Path ((a : A) * B a)
	      (pr1Exists A B x, pr2Exists A B x) x =
  refl ((a : A) * B a) x

indExistsFromPr
  (A : U) (B : A -> U) (C : ((x : A) * B x) -> U)
  (g : (x : A) -> (y : B x) -> C (x, y))
  (x : (x : A) * (B x)) : C x =
  transport (<i> C (uniqExists A B x @ i))
            (g (pr1Exists A B x) (pr2Exists A B x))

-- exercise 1.4
iter (C : U) (c0 : C) (cs : C -> C) : N -> C = split
  zero -> c0
  succ n -> cs (iter C c0 cs n)

iterStep (C : U) (cs : N -> C -> C) (p : Sigma N C) : Sigma N C =
  (succ p.1, cs p.1 p.2)

recNatFromIter' (C : U) (c0 : C) (cs : N -> C -> C) (n : N) :
                Sigma N C =
  iter (Sigma N C) (zero, c0) (iterStep C cs) n

recNatFromIter (C : U) (c0 : C) (cs : N -> C -> C) (n : N) : C =
  (recNatFromIter' C c0 cs n).2

double' : N -> N =
  recNatFromIter N zero (\(n : N) (y : N) -> succ (succ y))

recNatIterEq (C : U) (c0 : C) (cs : N -> C -> C) : (n : N) ->
  Path (Sigma N C) (n, recNat C c0 cs n)
                     (recNatFromIter' C c0 cs n) = split
  zero -> refl (Sigma N C) (zero, c0)
  succ n -> ?

-- exercise 1.5
CoproductB (A B : U) : U = (x : Bool) * (recBool U A B x)

inlB (A B : U) (a : A) : CoproductB A B = (false, a)

inrB (A B : U) (b : B) : CoproductB A B = (true, b)

indCoproductB (A B C : U) (C : CoproductB A B -> U)
              (g0 : (a : A) -> C (inlB A B a))
              (g1 : (b : B) -> C (inrB A B b))
              (x : CoproductB A B) : C x =
  indBool (\(x : Bool) -> (n : recBool U A B x) -> C (x, n))
          g0 g1 x.1 x.2

-- exercise 1.6
TupleB (A B : U) : U = (x : Bool) -> (recBool U A B x)

pairB (A B : U) (a : A) (b : B) : TupleB A B = split
  false -> a
  true -> b

pr1TupleB (A B : U) (x : TupleB A B) : A = x false
pr2TupleB (A B : U) (x : TupleB A B) : B = x true

funExt (A : U) (B : A -> U) (f g : (x : A) -> B x)
  (p : (x : A) -> Path (B x) (f x) (g x)) :
  Path ((x : A) -> B x) f g =
  <i> \(x : A) -> (p x) @ i

uniqTupleB (A B : U) (x : TupleB A B) :
         Path (TupleB A B) (pairB A B (pr1TupleB A B x)
                                      (pr2TupleB A B x)) x =
  funExt Bool (recBool U A B)
         (pairB A B (pr1TupleB A B x) (pr2TupleB A B x)) x hole
  where
    hole : (b : Bool) ->
     Path (recBool U A B b)
       ((pairB A B (pr1TupleB A B x) (pr2TupleB A B x)) b) (x b) = split
     false -> refl A (x false)
     true -> refl B (x true)

indTupleB (A B C : U) (C : TupleB A B -> U)
          (g : (x : A) -> (y : B) -> C (pairB A B x y))
          (x : TupleB A B) : C x =
  transport (<i> C (uniqTupleB A B x @ i))
            (g (pr1TupleB A B x) (pr2TupleB A B x))

-- exercise 1.8
multRec : N -> N -> N =
  recNat (N -> N) (\(_ : N) -> zero)
         (\(n : N) (g : N -> N) (m : N) -> addRec m (g m))

pownRecFlipped : N -> N -> N =
  recNat (N -> N) (\(_ : N) -> (succ zero))
         (\(n : N) (g : N -> N) (m : N) -> multRec m (g m))

pownRec (n : N) (m : N) : N = pownRecFlipped m n

-- Yes, we have already defined add—addRec—through
-- recursor of natural numbers,
-- but proofs with it is so ugly and complicated,
-- that I think, that add defined through
-- pattern‐matching is 100 times better.
add (m : N) : N -> N = split
  zero -> m
  succ n -> succ (add m n)

-- The same thing
mult : N -> N -> N = split
  zero -> const N N zero
  succ n -> \(m : N) ->
    add m (mult n m)

addZero : (a : N) -> Path N (add zero a) a = split
  zero -> <_> zero
  succ a -> <i> succ (addZero a @ i)

addSucc (a : N) : (b : N) ->
        Path N (add (succ a) b) (succ (add a b)) = split
  zero -> <_> succ a
  succ b -> <i> succ (addSucc a b @ i)

addComm (a : N) : (b : N) -> Path N (add a b) (add b a) = split
  zero -> <i> addZero a @ -i
  succ b -> <i> comp (refl U N) (succ (addComm a b @ i))
                     [ (i = 0) -> refl N (succ (add a b)),
                       (i = 1) -> <j> addSucc b a @ -j ]

zeroIdentityNat : hasIdentity N add zero =
  \(x : N) -> pair (addZero x) (refl N x)

addA (a b : N) : (c : N) ->
  Path N (add a (add b c)) (add (add a b) c) = split
  zero -> <_> add a b
  succ c -> <i> succ (addA a b c @ i)

multRightZero : (a : N) -> Path N zero (mult a zero) = split
  zero -> refl N zero
  succ a -> <i> comp (refl U N) ((addZero (mult a zero)) @ -i)
                     [ (i = 0) -> <j> (multRightZero a) @ -j,
		       (i = 1) -> refl N (add zero (mult a zero)) ]

moveSuccInAdd (a : N) : (b : N) ->
  Path N (add (succ a) b) (succ (add a b)) = split
  zero -> refl N (succ a)
  succ b -> <i> succ (moveSuccInAdd a b @ i)

multAOnSb : (a : N) -> (b : N) ->
  Path N (mult a (succ b)) (add (mult a b) a) = split
  zero -> \(b : N) -> refl N zero
  succ a -> \(b : N) ->
    <i> comp (refl U N) (moveSuccInAdd b (add (mult a b) a) @ i)
             [ (i = 0) -> <j> add (succ b) (multAOnSb a b @ -j),
               (i = 1) -> <j> succ (addA b (mult a b) a @ j) ]

multDistribRightSwapped (b c : N) : (a : N) ->
  Path N (mult (add a b) c) (add (mult a c) (mult b c)) = split
  zero -> <i> comp (refl U N) (mult (addZero b @ i) c)
                   [ (i = 0) -> refl N (mult (add zero b) c),
                     (i = 1) -> <j> addZero (mult b c) @ -j ]
  succ a ->
    <i> comp (refl U N) (add c (multDistribRightSwapped b c a @ i))
             [ (i = 0) -> expandSuccInMult,
	             (i = 1) -> addA c (mult a c) (mult b c) ]
    where
      hole0 :
        Path N (add c (mult (add a b) c)) (mult (succ (add a b)) c) =
        refl N (add c (mult (add a b) c))

      expandSuccInMult :
        Path N (add c (mult (add a b) c)) (mult (add (succ a) b) c) =
        <i> comp (refl U N) (hole0 @ i)
                 [ (i = 0) -> refl N (add c (mult (add a b) c)),
                   (i = 1) -> <j> mult (moveSuccInAdd a b @ -j) c ]

multDistribRight (a b c : N) :
  Path N (mult (add a b) c) (add (mult a c) (mult b c)) =
  multDistribRightSwapped b c a

multComm (a : N) : (b : N) -> Path N (mult a b) (mult b a) = split
  zero -> <i> multRightZero a @ -i
  succ b -> <i> comp (refl U N) (multAOnSb a b @ i)
                [ (i = 0) -> refl N (mult a (succ b)),
                  (i = 1) -> hole0 ]
    where
      hole0 : Path N (add (mult a b) a) (add a (mult b a)) =
        <i> comp (refl U N) (addComm (mult b a) a @ i)
                 [ (i = 0) -> <j> add (multComm b a @ j) a,
                   (i = 1) -> refl N (add a (mult b a))]

multDistribLeft (a b c : N) :
  Path N (mult a (add b c)) (add (mult a b) (mult a c)) =
  <i> comp (refl U N) (multDistribRight b c a @ i)
           [ (i = 0) -> multComm (add b c) a,
             (i = 1) -> <j> add (multComm b a @ j) (multComm c a @ j) ]

multIdentityRight (a : N) : Path N (mult a (succ zero)) a =
  multComm a (succ zero)

multAssoc (a b : N) : (c : N) ->
  Path N (mult a (mult b c)) (mult (mult a b) c) = split
  zero -> <i> comp (refl U N) (multRightZero a @ -i)
                   [ (i = 0) -> <j> mult a (multRightZero b @ j),
                     (i = 1) -> multRightZero (mult a b) ]
  succ c ->
    <i> comp (refl U N) (add (multAssoc a b c @ i) (mult a b))
             [ (i = 0) -> left,
               (i = 1) -> right ]
    where
      right : Path N (add (mult (mult a b) c) (mult a b))
                       (mult (mult a b) (succ c)) =
        <i> comp (refl U N)
                 (multDistribLeft (mult a b) c (succ zero) @ -i)
                 [ (i = 0) -> <j> add (mult (mult a b) c)
                                      (multIdentityRight (mult a b) @ j),
                   (i = 1) -> refl N (mult (mult a b)
                                       (add c (succ zero)))]
      hole0 : Path N (mult a (add (mult b c) (mult b (succ zero))))
                       (add (mult a (mult b c)) (mult a b)) =
        <i> comp (refl U N) (multDistribLeft a (mult b c) b @ i)
                 [ (i = 0) ->
                     <j> mult a (add (mult b c) (multIdentityRight b @ -j)),
                   (i = 1) -> refl N (add (mult a (mult b c)) (mult a b)) ]
      left : Path N (add (mult a (mult b c)) (mult a b))
                      (mult a (mult b (succ c))) =
        <i> comp (refl U N)
                 (mult a (multDistribLeft b c (succ zero) @ -i))
                 [ (i = 0) -> hole0,
                   (i = 1) -> refl N (mult a (mult b (succ c)))]

commutativeMonoidOverNatAdd : isCommutativeMonoid N =
  ((add, zero, addA, zeroIdentityNat), addComm)

semigroupOverNatMul : isSemigroup N =
  (mult, multAssoc)

semiringOverNat : isSemiring N =
  (commutativeMonoidOverNatAdd,
   semigroupOverNatMul,
   multDistribLeft,
   multDistribRight,
   \(x : N) -> refl N zero,
   \(x : N) -> multComm x zero)

natSemiring : Semiring = (N, semiringOverNat)

-- exercise 1.9
Fin : N -> U = undefined

-- exercise 1.10
ack : N -> N -> N =
  recNat (N -> N) (\(n : N) -> succ n) -- wtf?
    (\(_ : N) (g : N -> N) ->
      recNat N (g (succ zero)) (\(_ : N) -> g))

-- exercise 1.11
tripleNegation (A : U) (h : not (not (not A))) : not A =
  \(x : A) -> h (\(g : A -> Empty) -> g x)

-- exercise 1.12
theorem1 (A B : U) : A -> B -> A =
  \(a : A) (b : B) -> a

theorem2 (A : U) : A -> not (not A) =
  \(a : A) (g : A -> Empty) -> g a

theorem3 (A B : U) : Coproduct (not A) (not B) -> not (Tuple A B) = split
  inl a -> \(p : Tuple A B) -> a (pr1Tuple A B p)
  inr b -> \(p : Tuple A B) -> b (pr2Tuple A B p)

-- exercise 1.13
notLEM (P : U) : not (not (Coproduct P (not P))) =
  \(h : not (Coproduct P (not P))) -> h (inr (\(x : P) -> h (inl x)))

-- exercise 1.14
{-
f (A : U) (x : A) (p : Path A x x) : Path (Path A x x) p (refl A x) =
  indPath A (\(x y : A) (p : Path A x y) -> ???)
-}

-- exercise 1.15
uniqPath (A : U) (C : A -> U) (x y : A) (p : Path A x y) (h : C x) : C y =
  indPath A (\(x y : A) (p : Path A x y) -> (C x -> C y))
          (\(x : A) -> idfun (C x)) x y p h

-- exercise 1.16
-- 1.8
-- addComm (a : N) : (b : N) -> Path N (add a b) (add b a)
